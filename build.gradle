buildscript {

    ext {
        kotlin_version = "1.3.61"
        cucumber_version = "4.3.1"
        jfrogKey = System.getenv("JFROG_BINTRAY_ACCESS_KEY")
    }

    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.0.8.RELEASE"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.4"
}

group "albelli"
version = System.getenv("CI_BUILD_ID")
version version

apply plugin: "java"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

apply plugin: "kotlin"
apply plugin: "maven-publish"
apply plugin: "io.spring.dependency-management"

sourceSets {
    main {
        java {
            srcDirs = ["src"]
        }
    }
}

bintray {
    user = "derwasp"
    key = jfrogKey
    publications = ["MyPublication"]
    publish = true
    override = true // whatever for now
    pkg {
        repo = "random"
        name = "junit.synnefo"
        licenses = ["Apache-2.0"]
        vcsUrl = "https://github.com/albumprinter/junit-synnefo"
    }
}

dependencyManagement {
    imports {
        mavenBom "software.amazon.awssdk:bom:2.5.23"
    }
}

dependencies {
    compile "io.cucumber:cucumber-core:$cucumber_version"
    compile "io.cucumber:cucumber-java:$cucumber_version"
    compile "io.cucumber:cucumber-java8:$cucumber_version"
    compile "io.cucumber:cucumber-junit:$cucumber_version"
    compile "software.amazon.awssdk:s3:2.11.4"
    compile "software.amazon.awssdk:codebuild:2.5.23"
    compile "software.amazon.awssdk:sdk-core:2.5.23"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.3.3"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3"
}

task buildJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives buildJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact buildJar
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
